apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'jacoco-android'
apply from: rootProject.file('gradle/fabric-properties-generate.gradle')

allOpen {
    annotation("com.egugue.licol.common.AllOpen")
}

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "com.egugue.licol"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true //TODO
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"

            buildConfigField "String", "TWITTER_CONSUMER_KEY", "\"${System.getenv("LIKEDIT_FABRIC_TWITTER_CONSUMER_KEY")}\""
            buildConfigField "String", "TWITTER_CONSUMER_SECRET", "\"${System.getenv("LIKEDIT_FABRIC_TWITTER_CONSUMER_SECRET")}\""
        }
        release {
            buildConfigField "String", "TWITTER_CONSUMER_KEY", "\"${System.getenv("LIKEDIT_FABRIC_TWITTER_CONSUMER_KEY")}\""
            buildConfigField "String", "TWITTER_CONSUMER_SECRET", "\"${System.getenv("LIKEDIT_FABRIC_TWITTER_CONSUMER_SECRET")}\""
            minifyEnabled false //TODO
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    /*
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    */
    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
    packagingOptions {
        exclude 'META-INF/app_debug.kotlin_module'
    }
    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
    lintOptions {
        abortOnError false
        textReport true
        textOutput 'stdout'
        lintConfig file("lint.xml")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // https://developer.android.com/topic/libraries/support-library/revisions.html
    def supportLibVer = '26.0.0'
    implementation "com.android.support:appcompat-v7:$supportLibVer"
    implementation "com.android.support:recyclerview-v7:$supportLibVer"
    implementation "com.android.support:cardview-v7:$supportLibVer"
    implementation "com.android.support:design:$supportLibVer"
    implementation "com.android.support:customtabs:$supportLibVer"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // https://developer.android.com/topic/libraries/architecture/release-notes.html
    def archVer = "1.0.0-alpha7"
    implementation "android.arch.lifecycle:runtime:$archVer"
    implementation "android.arch.lifecycle:extensions:$archVer"
    kapt "android.arch.lifecycle:compiler:$archVer"

    // firebase https://firebase.google.com/support/release-notes/android
    def firebaseVer = '11.0.2'
    implementation "com.google.firebase:firebase-core:$firebaseVer"
    implementation "com.google.firebase:firebase-crash:$firebaseVer"

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:2.0.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle:2.0.1'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-android:2.0.1'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.0.1'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-kotlin:2.0.1'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

    // Dagger
    implementation 'com.google.dagger:dagger:2.11'
    kapt 'com.google.dagger:dagger-compiler:2.11'

    // View
    implementation 'com.airbnb.android:epoxy:2.1.0'

    // Network
    implementation 'com.squareup.okhttp3:okhttp:3.8.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'

    def retrofitVer = '2.2.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVer"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVer"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVer"

    // Shared Preferences
    implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC1'

    // SQLite
    // If it is missing, we can see error on CI https://github.com/square/sqldelight/issues/563
    implementation 'com.squareup.sqldelight:runtime:0.6.1'
    implementation 'com.squareup.sqlbrite2:sqlbrite:2.0.0'

    // Tools
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    implementation 'com.jakewharton.timber:timber:4.5.1'
    implementation('com.twitter.sdk.android:twitter:2.3.2@aar') {
        transitive = true
    }
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }

    // debug
    debugImplementation 'com.facebook.stetho:stetho:1.4.2'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.4.2'
    debugImplementation "com.github.brianPlummer:tinydancer:0.1.1"
    debugImplementation 'jp.wasabeef:takt:1.0.5'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.7.21'
    testImplementation 'com.google.truth:truth:0.32'
    testImplementation 'com.nhaarman:mockito-kotlin:1.4.0'

    // when updating Robolectric, see gradle.properties
    testImplementation 'org.robolectric:robolectric:3.3.2'

    androidTestImplementation("com.android.support.test.espresso:espresso-core:2.2.2") {
        exclude module: 'jsr305'
    }
    androidTestImplementation 'com.android.support.test:runner:1.0.0'
    androidTestImplementation "com.android.support:support-annotations:$supportLibVer"
    androidTestImplementation 'com.google.truth:truth:0.32'
}

jacocoAndroidUnitTestReport {
    excludes += [
            // sqlibrite
            '**/sqlite/*Model*.*',

            // dagger
            '**/*Module*.*',
            '**/Dagger*.*',
            '**/*_Factory.*',
            '**/*_MembersInjector.*',

            // Android
            '**/App.*',
            '**/DebugApp.*',
            '**/MainActivity*',
            '**/ui/**',
            '**/TweetSyncService.*',
            '**/databinding/*',
    ]
}
