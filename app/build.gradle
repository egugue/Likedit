apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: 'jacoco-android'
apply from: rootProject.file('gradle/fabric-properties-generate.gradle')

repositories {
    mavenCentral()
    maven { url "https://maven.google.com" }
    maven { url 'https://maven.fabric.io/public' }
}

allOpen {
    annotation("com.egugue.licol.common.AllOpen")
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "com.egugue.licol"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true //TODO
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"

            buildConfigField "String", "TWITTER_CONSUMER_KEY", "\"${System.getenv("LIKEDIT_FABRIC_TWITTER_CONSUMER_KEY")}\""
            buildConfigField "String", "TWITTER_CONSUMER_SECRET", "\"${System.getenv("LIKEDIT_FABRIC_TWITTER_CONSUMER_SECRET")}\""
        }
        release {
            buildConfigField "String", "TWITTER_CONSUMER_KEY", "\"${System.getenv("LIKEDIT_FABRIC_TWITTER_CONSUMER_KEY")}\""
            buildConfigField "String", "TWITTER_CONSUMER_SECRET", "\"${System.getenv("LIKEDIT_FABRIC_TWITTER_CONSUMER_SECRET")}\""
            minifyEnabled false //TODO
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    /*
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    */
    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
    //TODO: remove when only use rxjava2
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/app_debug.kotlin_module'
    }
    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
    lintOptions {
        abortOnError false
        textReport true
        textOutput 'stdout'
        lintConfig file("lint.xml")
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // https://developer.android.com/topic/libraries/support-library/revisions.html
    def supportLibVer = '26.0.0-beta2'
    compile "com.android.support:appcompat-v7:$supportLibVer"
    compile "com.android.support:recyclerview-v7:$supportLibVer"
    compile "com.android.support:design:$supportLibVer"
    compile "com.android.support:customtabs:$supportLibVer"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    // firebase https://firebase.google.com/support/release-notes/android#20170428
    def firebaseVer = '10.2.4'
    compile "com.google.firebase:firebase-core:$firebaseVer"
    compile "com.google.firebase:firebase-crash:$firebaseVer"

    // RxJava
    compile 'io.reactivex.rxjava2:rxjava:2.0.9'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    compile 'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:2.0.0'
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle-android:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle-kotlin:2.0.1'

    // Dagger
    compile 'com.google.dagger:dagger:2.10'
    kapt 'com.google.dagger:dagger-compiler:2.10'

    // View
    def butterknifeVer = "8.6.0"
    compile "com.jakewharton:butterknife:$butterknifeVer"
    kapt "com.jakewharton:butterknife-compiler:$butterknifeVer"

    compile 'com.airbnb.android:epoxy:2.1.0'

    // Network
    compile 'com.squareup.okhttp3:okhttp:3.7.0'

    def retrofitVer = '2.2.0'
    compile "com.squareup.retrofit2:retrofit:$retrofitVer"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVer"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofitVer"

    // Shared Preferences
    compile 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC1'

    // SQLite
    // If it is missing, we can see error on CI https://github.com/square/sqldelight/issues/563
    compile 'com.squareup.sqldelight:runtime:0.6.1'
    compile 'com.squareup.sqlbrite:sqlbrite:1.1.1'
    compile "com.github.akarnokd:rxjava2-interop:0.9.6"

    // Tools
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile('com.twitter.sdk.android:twitter:2.3.2@aar') {
        transitive = true
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }

    // debug
    debugCompile 'com.facebook.stetho:stetho:1.4.2'
    debugCompile 'com.facebook.stetho:stetho-okhttp3:1.4.2'
    debugCompile "com.github.brianPlummer:tinydancer:0.1.1"

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.21'
    testCompile 'com.google.truth:truth:0.32'
    testCompile 'com.nhaarman:mockito-kotlin:1.4.0'

    // when updating Robolectric, see gradle.properties
    testCompile 'org.robolectric:robolectric:3.3.2'

    androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2") {
        exclude module: 'jsr305'
    }
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile "com.android.support:support-annotations:$supportLibVer"
    androidTestCompile 'com.google.truth:truth:0.32'
}

jacocoAndroidUnitTestReport {
    excludes += [
            // sqlibrite
            '**/sqlite/*Model*.*',

            // dagger
            '**/*Module*.*',
            '**/Dagger*.*',
            '**/*_Factory.*',
            '**/*_MembersInjector.*',

            // Android
            '**/App.*',
            '**/DebugApp.*',
            '**/MainActivity*',
            '**/ui/**',
            '**/TweetSyncService.*',
            '**/databinding/*',
    ]
}
